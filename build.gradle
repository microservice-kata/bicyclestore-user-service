buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.1.0"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.10.RELEASE"
        classpath "gradle.plugin.com.boxfuse.client:gradle-plugin-publishing:5.0.7"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "org.junit.platform.gradle.plugin"
apply plugin: "org.springframework.boot"

apply {
    from("tasks/flyway.gradle")
    from("tasks/test-coverage.gradle")
    from("tasks/checkstyle.gradle")
    from("tasks/sonarqube.gradle")
}


sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    providedRuntime
}

ext {
    junitJupiterVersion = "5.1.0"
    junitPlatformVersion = "1.1.0"
    jjwtVersion = "0.9.0"
    mysqlConnectorVersion = "6.0.6"
    lombokVersion = "1.16.20"
    springExtensionVersion = "1.0.3"
    springMockMvCVersion = "2.9.0"
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("io.jsonwebtoken:jjwt:${jjwtVersion}")

    compile("mysql:mysql-connector-java:${mysqlConnectorVersion}")
    compile("org.projectlombok:lombok:${lombokVersion}")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile('org.springframework.security:spring-security-test')
    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testCompile("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    testCompile("org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")
    testCompile("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")
    testCompile("org.junit.platform:junit-platform-runner:${junitPlatformVersion}")
    testCompile("com.jayway.restassured:spring-mock-mvc:${springMockMvCVersion}")
    testCompile("com.github.sbrannen:spring-test-junit5:${springExtensionVersion}")
    testCompile("com.h2database:h2:1.4.192")
}



def env = project.hasProperty("env") ? env : "dev"

processResources {
    println("active profile: ${env}")

    include "application.yml"
    include "application-${env}.yml"
    include "db/migration/**"
}
build.dependsOn processResources
bootRun.dependsOn flywayMigrate
bootRun.systemProperty "spring.profiles.active", env
